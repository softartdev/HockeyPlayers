plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'com.squareup.sqldelight'
}
sqldelight {
    HockeyDb {
        packageName = "com.example.sqldelight.hockey"
    }
}
kotlin {
    android()
    // Configure iOS.
    def sdkName = System.getenv("SDK_NAME")
    if (sdkName != null && sdkName.startsWith("iphoneos")) {
        iosArm64("ios")
    } else {
        iosX64("ios") {
            binaries {
                framework {
                    baseName = 'shared'
                }
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib-jdk7')
                implementation 'androidx.core:core-ktx:1.3.0'
                implementation "com.squareup.sqldelight:android-driver:$sqldelight_version"
            }
        }
        androidTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
                implementation "com.squareup.sqldelight:sqlite-driver:$sqldelight_version"
            }
        }
        iosMain {
            dependencies {
                implementation "com.squareup.sqldelight:native-driver:$sqldelight_version"
            }
        }
        iosTest {
        }
    }
}
android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        'release' {
            minifyEnabled false
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
task(packForXcode, type: Sync) {
    group = 'build'
    def mode = System.getenv('CONFIGURATION') ?: 'DEBUG'
    def framework = kotlin.targets.ios.binaries.getFramework(mode)
    inputs.property('mode', mode)
    dependsOn(framework.linkTask)
    def targetDir = new File(buildDir, 'xcode-frameworks')
    from({ framework.outputDirectory })
    into(targetDir)
}
tasks.getByName('build').dependsOn(packForXcode)